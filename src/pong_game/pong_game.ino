#include <Gamebuino.h>
#include <SPI.h>

Gamebuino game_buino_;

// PLAYERS DATA
int player_score_01_ = 0;
int player_score_02_ = 0;
const int kWinConditionScore = 11;
const int kPlayerHeight = 16;  // this is 1/3 MAKERbuino screen
const int kPlayerWidth = 3;
const int kPlayerVelocityY = 2;
const int kTopScreenOffset = LCDHEIGHT - kPlayerHeight;
int player_01_x_ = 0;
int player_01_y_ = kTopScreenOffset / 2;
int player_02_x_ = LCDWIDTH - kPlayerWidth;
int player_02_y_ = player_01_y_;

// BALL DATA
const int kBallSize = 4;
const int kBallVelocity = 3;
int ball_x_ = (LCDWIDTH - kBallSize) / 2;
int ball_y_ = player_01_y_;
int ball_velocity_x_ = kBallVelocity;
int ball_velocity_y_ = kBallVelocity;

// LINES DATA
const int kNumberOfLines = 12;
const int kLineWidth = 2;
const int kLineHeight = LCDHEIGHT / kNumberOfLines;
const int kLineX = (LCDWIDTH - kLineWidth) / 2;

// BORDERS DATA
const int kBorderThickness = 1;

// HELPER VARIABLES
const int kOffsetFromPlayer01 = player_01_x_ + kPlayerWidth + kBallSize + 1;
const int kOffsetFromPlayer02 = player_02_x_ - kBallSize - 1;

// IMAGES
const byte kTitleBitmap[] PROGMEM = {
    64,   30,   0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x03, 0xff, 0xff, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x02, 0x00, 0x01, 0x00, 0x00, 0x00, 0x00, 0x00, 0x02, 0x00,
    0x01, 0x00, 0x00, 0x00, 0x00, 0x00, 0x02, 0x00, 0x01, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x02, 0x00, 0x01, 0x00, 0x00, 0x00, 0x00, 0x00, 0x02, 0x00,
    0x01, 0x00, 0x00, 0x00, 0x00, 0x00, 0x02, 0x00, 0x01, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x02, 0x00, 0x01, 0x00, 0x00, 0x00, 0x00, 0x00, 0x02, 0x00,
    0x01, 0x00, 0x00, 0x00, 0x00, 0x00, 0x02, 0x00, 0x01, 0x00, 0x00, 0x00,
    0x00, 0x00, 0x03, 0xff, 0xff, 0x00, 0x00, 0x00, 0x00, 0x00, 0x02, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x02, 0x00, 0x01, 0xff, 0xe7, 0xfc,
    0xff, 0x80, 0x02, 0x00, 0x01, 0x00, 0x24, 0x04, 0x80, 0x80, 0x02, 0x00,
    0x01, 0x00, 0x24, 0x04, 0x80, 0x80, 0x02, 0x00, 0x01, 0x00, 0x24, 0x04,
    0x80, 0x80, 0x02, 0x00, 0x01, 0x00, 0x24, 0x04, 0x80, 0x80, 0x02, 0x00,
    0x01, 0x00, 0x24, 0x04, 0x80, 0x80, 0x02, 0x00, 0x01, 0x00, 0x24, 0x04,
    0x80, 0x80, 0x02, 0x00, 0x01, 0xff, 0xe4, 0x04, 0xff, 0x80, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x80, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x80, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x80, 0x00, 0x00,
    0x00, 0x00, 0x00, 0x00, 0x00, 0x80, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
    0xff, 0x80};

const byte kPlayer01WinsBitmap[] PROGMEM = {
    64,   30,   0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x07, 0xff,
    0xe4, 0x00, 0x01, 0x88, 0x00, 0x08, 0x04, 0x00, 0x24, 0x00, 0x02, 0x84,
    0x00, 0x10, 0x04, 0x00, 0x24, 0x00, 0x04, 0x82, 0x00, 0x20, 0x04, 0x00,
    0x24, 0x00, 0x08, 0x81, 0x00, 0x40, 0x04, 0x00, 0x24, 0x00, 0x10, 0x80,
    0x88, 0x80, 0x04, 0x00, 0x24, 0x00, 0x20, 0x80, 0x55, 0x00, 0x04, 0x00,
    0x24, 0x00, 0x40, 0x80, 0x22, 0x00, 0x04, 0x00, 0x24, 0x00, 0x80, 0x80,
    0x00, 0x00, 0x04, 0x00, 0x24, 0x00, 0x00, 0x80, 0x08, 0x00, 0x04, 0x00,
    0x24, 0x00, 0x00, 0x80, 0x08, 0x00, 0x04, 0x00, 0x24, 0x00, 0x00, 0x80,
    0x08, 0x00, 0x07, 0xff, 0xe4, 0x00, 0x00, 0x80, 0x08, 0x00, 0x04, 0x00,
    0x04, 0x00, 0x00, 0x80, 0x08, 0x00, 0x04, 0x00, 0x04, 0x00, 0x00, 0x80,
    0x08, 0x00, 0x04, 0x00, 0x04, 0x00, 0x00, 0x80, 0x08, 0x00, 0x04, 0x00,
    0x04, 0x00, 0x00, 0x80, 0x08, 0x00, 0x04, 0x00, 0x04, 0x00, 0x00, 0x80,
    0x08, 0x00, 0x04, 0x00, 0x04, 0x00, 0x00, 0x80, 0x00, 0x00, 0x04, 0x00,
    0x04, 0x00, 0x00, 0x81, 0x80, 0x20, 0x04, 0x00, 0x04, 0x00, 0x00, 0x81,
    0x40, 0x20, 0x04, 0x00, 0x04, 0x00, 0x00, 0x81, 0x20, 0x20, 0x04, 0x00,
    0x04, 0x00, 0x00, 0x81, 0x10, 0x20, 0x04, 0x00, 0x04, 0x00, 0x00, 0x81,
    0x08, 0x20, 0x04, 0x00, 0x04, 0x00, 0x00, 0x81, 0x04, 0x20, 0x04, 0x00,
    0x04, 0x00, 0x00, 0x81, 0x02, 0x20, 0x04, 0x00, 0x04, 0x00, 0x00, 0x81,
    0x01, 0x20, 0x04, 0x00, 0x04, 0x00, 0x00, 0x81, 0x00, 0xa0, 0x04, 0x00,
    0x07, 0xff, 0x00, 0x81, 0x00, 0x60, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00};

const byte kPlayer02WinsBitmap[] PROGMEM = {
    64,   30,   0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00, 0x07, 0xff,
    0xe4, 0x00, 0x00, 0x08, 0x00, 0x08, 0x04, 0x00, 0x24, 0x00, 0x00, 0x04,
    0x00, 0x10, 0x04, 0x00, 0x24, 0x00, 0x0e, 0x02, 0x00, 0x20, 0x04, 0x00,
    0x24, 0x00, 0x11, 0x01, 0x00, 0x40, 0x04, 0x00, 0x24, 0x00, 0x20, 0x80,
    0x88, 0x80, 0x04, 0x00, 0x24, 0x00, 0x40, 0x40, 0x55, 0x00, 0x04, 0x00,
    0x24, 0x00, 0x80, 0x20, 0x22, 0x00, 0x04, 0x00, 0x24, 0x01, 0x00, 0x40,
    0x00, 0x00, 0x04, 0x00, 0x24, 0x01, 0x00, 0x80, 0x08, 0x00, 0x04, 0x00,
    0x24, 0x00, 0x01, 0x00, 0x08, 0x00, 0x04, 0x00, 0x24, 0x00, 0x02, 0x00,
    0x08, 0x00, 0x07, 0xff, 0xe4, 0x00, 0x04, 0x00, 0x08, 0x00, 0x04, 0x00,
    0x04, 0x00, 0x08, 0x00, 0x08, 0x00, 0x04, 0x00, 0x04, 0x00, 0x10, 0x00,
    0x08, 0x00, 0x04, 0x00, 0x04, 0x00, 0x20, 0x20, 0x08, 0x00, 0x04, 0x00,
    0x04, 0x00, 0x40, 0x20, 0x08, 0x00, 0x04, 0x00, 0x04, 0x00, 0x7f, 0xe0,
    0x08, 0x00, 0x04, 0x00, 0x04, 0x00, 0x00, 0x00, 0x00, 0x00, 0x04, 0x00,
    0x04, 0x00, 0x00, 0x01, 0x80, 0x20, 0x04, 0x00, 0x04, 0x00, 0x00, 0x01,
    0x40, 0x20, 0x04, 0x00, 0x04, 0x00, 0x00, 0x01, 0x20, 0x20, 0x04, 0x00,
    0x04, 0x00, 0x00, 0x01, 0x10, 0x20, 0x04, 0x00, 0x04, 0x00, 0x00, 0x01,
    0x08, 0x20, 0x04, 0x00, 0x04, 0x00, 0x00, 0x01, 0x04, 0x20, 0x04, 0x00,
    0x04, 0x00, 0x00, 0x01, 0x02, 0x20, 0x04, 0x00, 0x04, 0x00, 0x00, 0x01,
    0x01, 0x20, 0x04, 0x00, 0x04, 0x00, 0x00, 0x01, 0x00, 0xa0, 0x04, 0x00,
    0x07, 0xff, 0x00, 0x01, 0x00, 0x60, 0x00, 0x00, 0x00, 0x00, 0x00, 0x00,
    0x00, 0x00};

void ResetGame() {
  game_buino_.battery.show = false;
  game_buino_.display.fontSize = 2;
  player_score_01_ = 0;
  player_score_02_ = 0;
  player_01_y_ = kTopScreenOffset / 2;
  player_02_y_ = player_01_y_;
  ball_x_ = (LCDWIDTH - kBallSize) / 2;
  ball_y_ = player_01_y_;
  ball_velocity_x_ = kBallVelocity * (random(0, 2) == 0 ? 1 : -1);
  ball_velocity_y_ = kBallVelocity * (random(0, 2) == 0 ? 1 : -1);
}

void InitGame() {
  game_buino_.titleScreen(F(""), kTitleBitmap);  //
  game_buino_.titleScreen(F("Pong for 2 players\n BY: Rok Kos"));
  game_buino_.pickRandomSeed();
  ResetGame();
}

void setup() {
  // put your setup code here, to run once:
  game_buino_.begin();
  InitGame();
}

void ProcesInputFromPlayers() {
  if (game_buino_.buttons.repeat(BTN_UP, 1)) {
    player_01_y_ = max(0, player_01_y_ - kPlayerVelocityY);
  }
  if (game_buino_.buttons.repeat(BTN_DOWN, 1)) {
    player_01_y_ = min(kTopScreenOffset, player_01_y_ + kPlayerVelocityY);
  }

  if (game_buino_.buttons.repeat(BTN_B, 1)) {
    player_02_y_ = max(0, player_02_y_ - kPlayerVelocityY);
  }
  if (game_buino_.buttons.repeat(BTN_A, 1)) {
    player_02_y_ = min(kTopScreenOffset, player_02_y_ + kPlayerVelocityY);
  }
}

void MoveBall() {
  ball_x_ += ball_velocity_x_;
  ball_y_ += ball_velocity_y_;

  bool is_ball_under_screen = ball_y_ + kBallSize > LCDHEIGHT;
  if (is_ball_under_screen) {
    ball_velocity_y_ *= -1;
    ball_y_ = LCDHEIGHT - kBallSize;
    game_buino_.sound.playTick();
  }

  bool is_ball_over_screen = ball_y_ - kBallSize < 0;
  if (is_ball_over_screen) {
    ball_velocity_y_ *= -1;
    ball_y_ = kBallSize;
    game_buino_.sound.playTick();
  }

  bool is_ball_touching_player_01_paddle =
      ball_x_ - kBallSize < player_01_x_ + kPlayerWidth &&
      ball_y_ <= player_01_y_ + kPlayerHeight && ball_y_ > player_01_y_;
  if (is_ball_touching_player_01_paddle) {
    ball_velocity_x_ *= -1;
    ball_x_ = kOffsetFromPlayer01;
    game_buino_.sound.playTick();
  }

  bool is_ball_touching_player_02_paddle =
      ball_x_ + kBallSize > player_02_x_ &&
      ball_y_ <= player_02_y_ + kPlayerHeight && ball_y_ > player_02_y_;
  if (is_ball_touching_player_02_paddle) {
    ball_velocity_x_ *= -1;
    ball_x_ = kOffsetFromPlayer02;
    game_buino_.sound.playTick();
  }
}

void PlayersScoring() {
  bool reset_round = false;

  bool is_ball_behind_player_01 = ball_x_ + kBallSize < player_01_x_;
  if (is_ball_behind_player_01) {
    reset_round = true;
    ball_x_ = kOffsetFromPlayer01;
    ball_velocity_x_ = kBallVelocity;
    player_score_02_++;
  }

  bool is_ball_behind_player_02 =
      ball_x_ - kBallSize > player_02_x_ + kPlayerWidth;
  if (is_ball_behind_player_02) {
    reset_round = true;
    ball_x_ = kOffsetFromPlayer02;
    ball_velocity_x_ = -kBallVelocity;
    player_score_01_++;
  }

  if (reset_round) {
    ball_y_ = random(0, LCDHEIGHT - kBallSize);
    game_buino_.sound.playCancel();
  }
}

void PrintScore() {
  game_buino_.display.cursorX = 21;
  game_buino_.display.cursorY = 8;
  game_buino_.display.print(player_score_01_);

  game_buino_.display.cursorX = 57;
  game_buino_.display.cursorY = 8;
  game_buino_.display.print(player_score_02_);
}

void JudgeSystem() {
  if (player_score_01_ < kWinConditionScore &&
      player_score_02_ < kWinConditionScore) {
    return;
  }

  if (player_score_01_ >= kWinConditionScore &&
      player_score_01_ - player_score_02_ >= 2) {
    game_buino_.titleScreen(F(""), kPlayer01WinsBitmap);
    ResetGame();
  } else if (player_score_02_ >= kWinConditionScore &&
             player_score_02_ - player_score_01_ >= 2) {
    game_buino_.titleScreen(F(""), kPlayer02WinsBitmap);
    ResetGame();
  }
}

void DrawGameElements() {
  // Draw players
  game_buino_.display.fillRect(player_01_x_, player_01_y_, kPlayerWidth,
                               kPlayerHeight);
  game_buino_.display.fillRect(player_02_x_, player_02_y_, kPlayerWidth,
                               kPlayerHeight);

  // Draw Ball
  game_buino_.display.fillCircle(ball_x_, ball_y_, kBallSize);

  // Draw middle lines
  for (int i = 0; i < kNumberOfLines; i += 2) {
    game_buino_.display.fillRect(kLineX, i * kLineHeight, kLineWidth,
                                 kLineHeight);
  }

  // Draw borders lines
  game_buino_.display.fillRect(0, 0, LCDWIDTH, kBorderThickness);
  game_buino_.display.fillRect(0, LCDHEIGHT - 1, LCDWIDTH, kBorderThickness);
}

void CheckForExitGame() {
  if (game_buino_.buttons.pressed(BTN_C)) {
    InitGame();
  }
}

void loop() {
  // put your main code here, to run repeatedly:

  // update returns true every 50 milisecons, this means we have 20 FPS
  if (game_buino_.update()) {
    ProcesInputFromPlayers();
    MoveBall();
    DrawGameElements();
    PlayersScoring();
    PrintScore();
    JudgeSystem();
    CheckForExitGame();
  }
}
